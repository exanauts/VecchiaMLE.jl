"""
    diagnostics, result = VecchiaMLE_Run(iVecchiaMLE::VecchiaMLEInput)

    Main function to run the analysis. Make sure that your VecchiaMLEInput struct is 
    filled out correctly!

## Input arguments
* `iVecchiaMLE`: The filled out VecchiaMLEInput struct. See VecchiaMLEInput struct for more details.

## Output arguments
* `diagnostics`: Some diagnostics of the analysis that would be difficult otherwise to get. See Diagnostics struct for more details.
* `pres_chol`: The approximate precision's cholesky, generated by the samples. 
"""
function VecchiaMLE_Run(iVecchiaMLE::VecchiaMLEInput)

    !iVecchiaMLE.skip_check && sanitize_input!(iVecchiaMLE)
    pres_chol = spzeros(iVecchiaMLE.n, iVecchiaMLE.n)
    diagnostics = Diagnostics(0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, cpu)
    VecchiaMLE_Run_Analysis!(iVecchiaMLE, pres_chol, diagnostics)
    
    return diagnostics, pres_chol
end

"""
See VecchiaMLE_Run().
"""
function VecchiaMLE_Run_Analysis!(iVecchiaMLE::VecchiaMLEInput, pres_chol::AbstractMatrix, diagnostics::Diagnostics)
    model, output = ExecuteModel!(iVecchiaMLE, pres_chol, diagnostics)
    
    !iVecchiaMLE.diagnostics && RetrieveDiagnostics!(iVecchiaMLE, output, model, diagnostics)
end

"""
See VecchiaMLE_Run().
"""
function ExecuteModel!(iVecchiaMLE::VecchiaMLEInput, pres_chol::AbstractMatrix, diags::Diagnostics)
    diags.create_model_time = @elapsed begin
        model = get_vecchia_model(iVecchiaMLE)
    end
    
    diags.solve_model_time = @elapsed begin
        output = madnlp(model, 
            #linear_solver=MadNLPHSL.Ma57Solver, # Linear Solver should be determined if found on machine! #TODO: Later
            print_level=iVecchiaMLE.pLevel,
            tol=1e-8
        )
    end
    
    valsL = view(output.solution, 1:model.cache.nnzL)
    rowsL = view(model.cache.rowsL, :)
    colsL = view(model.cache.colsL, :)

    # Casting to CPU matrices
    if iVecchiaMLE.mode != cpu
        valsL = Vector{Float64}(valsL)
        rowsL = Vector{Int}(rowsL)
        colsL = Vector{Int}(colsL)
    end

    pres_chol .= sparse(rowsL, colsL, valsL)


    return model, output
end

"""
See VecchiaMLE_Run()
"""
function RetrieveDiagnostics!(iVecchiaMLE, output, model, diagnostics)
    diagnostics.LinAlg_solve_time = output.counters.linear_solver_time
    diagnostics.MadNLP_iterations = output.iter
    diagnostics.mode = iVecchiaMLE.mode

    # Getting some values for error checking
    diagnostics.objective_value = NLPModels.obj(model, output.solution)
    cons_vec = typeof(output.solution)(undef, model.cache.n)
    NLPModels.cons!(model, output.solution, cons_vec)

    grad_vec = typeof(output.solution)(undef, length(output.solution))
    fill!(grad_vec, 0.0)
    NLPModels.grad!(model, output.solution, grad_vec)
    Jtv = similar(grad_vec)
    NLPModels.jtprod!(model, output.solution, output.multipliers, Jtv)
    grad_vec .+= Jtv

    diagnostics.normed_constraint_value = norm(cons_vec)
    diagnostics.normed_grad_value = norm(grad_vec)

end
